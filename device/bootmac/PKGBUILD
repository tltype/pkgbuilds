_mode=cross
_nodeps=true
pkgname=bootmac
pkgdesc="Configure MAC addresses at boot"
pkgver=0.6.0.r0.gf699dba
pkgrel=1
_arches=specific
arch=(
    aarch64
)
license=(GPL-3.0-or-later)
url=https://gitlab.postmarketos.org/postmarketOS/bootmac
depends=(
    bluez-utils
    bluez-deprecated-tools
)
_commit=f699dbac1e25edefe942b59996225b6d3af30c06
source=(
    "git+https://gitlab.postmarketos.org/postmarketOS/bootmac.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd $pkgname
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
    install -Dm644 "$pkgname"/bootmac-bluetooth.rules \
            "$pkgdir"/usr/lib/udev/rules.d/90-bootmac-bluetooth.rules
    install -Dm644 "$pkgname"/bootmac-wifi.rules \
            "$pkgdir"/usr/lib/udev/rules.d/90-bootmac-wifi.rules
    install -Dm755 "$pkgname"/bootmac "$pkgdir"/usr/bin/bootmac
    install -Dm644 "$pkgname"/bootmac-bluetooth.service \
            -t "$pkgdir"/usr/lib/systemd/system/
}
