_mode=cross
_nodeps=true
pkgbase=device-sdm845-xiaomi-beryllium
pkgname=(
    device-sdm845-xiaomi-beryllium-common
    device-sdm845-xiaomi-beryllium-ebbg
    device-sdm845-xiaomi-beryllium-tianma
)
pkgver=0.7
pkgrel=1
_arches=specific
arch=(aarch64)
license=(MIT)
makedepends=(devicepkg-helpers)
_commit=9550de18ec3b9ac2549a5aed196e705acf240afe
_commit_conf=2decd7eb54475fed90a6d55be9d1885642a26ecb #deviceinfo commit has different incompatible version
source=(
    "https://gitlab.postmarketos.org/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-beryllium/deviceinfo"
    "https://gitlab.postmarketos.org/postmarketOS/pmaports/-/raw/${_commit_conf}/device/community/device-xiaomi-beryllium/q6voiced.conf"
    "https://gitlab.postmarketos.org/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-beryllium/81-libssc-xiaomi-beryllium.rules"
    "https://gitlab.postmarketos.org/postmarketOS/pmaports/-/raw/${_commit}/device/community/device-xiaomi-beryllium/hexagonrpcd.confd"
    xiaomi-beryllium.conf
    machine-info
    rootston.ini
)
sha256sums=(
    2210b5cace07eb52974e9f32a3673adbcb0905a9c826e483f4fea6f12fb4bb61
    21b8395e7729c785c0446be5d88e52873b0fa46812111b91bcd5c3af7dde24da
    cd8207ad1532669306f403cad22c5730e64f3a2a70d58e089225527acce059e2
    9f3dc4f9e14e49be1e54e2ae4c0ee498b50ab38569d9d0781d610da7eef84cf6
    6591192b2696f0b6594e5c8724075752158eab2ea537d77c4d9bc3ead61f305e
    7729d6a89258b470cadd48cfbc704bab7791501c53f45f50737dea6b441a3171
    3ecab866e26f244bd0e449f6b56b109f362aed3fa6c95738428b222cc5766cd6
)

package_device-sdm845-xiaomi-beryllium-common() {
    depends=(
        device-sdm845-common
        firmware-sdm845-xiaomi-beryllium
    )
    install -Dm644 "$srcdir"/xiaomi-beryllium.conf "$pkgdir"/etc/kupfer/mkinitcpio.conf.d/xiaomi-beryllium.conf
    install -Dm644 "$srcdir"/machine-info "$pkgdir"/etc/machine-info
    install -Dm644 "$srcdir"/rootston.ini "$pkgdir"/etc/phosh/rootston.ini
    install -Dm644 "$srcdir"/q6voiced.conf "$pkgdir"/etc/conf.d/q6voiced
    install -Dm644 "$srcdir"/81-libssc-xiaomi-beryllium.rules -t "$pkgdir"/usr/lib/udev/rules.d/
    install -Dm644 "$srcdir"/hexagonrpcd.confd "$pkgdir"/etc/conf.d/hexagonrpcd-sdsp
}

package_device() {
    local variant="$1"
    cp "$srcdir"/deviceinfo "$srcdir"/deviceinfo_
    filter_deviceinfo "$variant" "$srcdir"/deviceinfo_
    cat >>"$srcdir"/deviceinfo_ <<EOF

deviceinfo_partitions_data="/dev/sda21"
deviceinfo_partitions_microsd="/dev/mmcblk0"
EOF

    if [ $variant = "ebbg" ]; then
        cat >>"$srcdir"/deviceinfo_ <<EOF

deviceinfo_modules_initfs="gpi i2c_qcom_geni qcom_pmi8998_charger qcom_fg edt_ft5x06"
EOF
    elif [ $variant = "tianma" ]; then
        cat >>"$srcdir"/deviceinfo_ <<EOF

deviceinfo_modules_initfs="gpi i2c_qcom_geni qcom_pmi8998_charger qcom_fg novatek_nvt_ts"
EOF
    fi

    install -Dm644 "$srcdir"/deviceinfo_ "$pkgdir"/etc/kupfer/deviceinfo
}

package_device-sdm845-xiaomi-beryllium-ebbg() {
    depends=(device-sdm845-xiaomi-beryllium-common)
    pkgdesc="Xiaomi Poco F1: EBBG panel edition"
    package_device ebbg
}

package_device-sdm845-xiaomi-beryllium-tianma() {
    depends=(device-sdm845-xiaomi-beryllium-common)
    pkgdesc="Xiaomi Poco F1: Tianma panel edition"
    package_device tianma
}
