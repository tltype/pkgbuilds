# Maintainer: Raihan Ahamed (raihan2000) <raihan1999ahamed@gmail.com>
_mode=host
_crossdirect=false
pkgname=libssc
pkgdesc="Library to expose Qualcomm Sensor Core sensors"
pkgver=0.2.2.r0.g8c706d4
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0)
url=https://codeberg.org/DylanVanAssche/libssc
provides=(libssc.so)
depends=(
    "libqmi>=1.33.4"
    glib2
    protobuf-c
)
makedepends=(
    git
    linux-headers
    meson
    protobuf
    qrtr-git
    python
    python-gobject
    python-protobuf
)
_commit=8c706d4805bf5b69245aaa483b27844385fe3df3 #tag 0.2.2
source=("git+https://codeberg.org/DylanVanAssche/libssc.git#commit=$_commit")
sha256sums=(3258ebf28f723c0f87c4a641ab513ed88ec037c827b7d20e040bb5144d2d15b3)

pkgver() {
    cd libssc
    git describe --long --abbrev=7 --tags $_commit | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson libssc build
    meson compile -C build
}

package() {
    DESTDIR="$pkgdir" meson install --no-rebuild -C build
}
