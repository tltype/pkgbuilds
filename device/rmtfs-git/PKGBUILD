_mode=host
_crossdirect=false
_pkgname=rmtfs
pkgname="$_pkgname-git"
pkgdesc="Qualcomm Remote Filesystem Service Implementation"
pkgver=1.1.1.r2.g586372e
pkgrel=1
_arches=specific
arch=(aarch64)
license=(BSD-3-Clause)
url="https://github.com/linux-msm/$_pkgname"
provides=("$_pkgname")
depends=(
    qrtr-git
    udev
)
_commit=586372e575f5cc1a7cbb170219ab6df98f394cae
source=(
    "git+$url#commit=$_commit"
    udev.rules
)
sha256sums=(
    SKIP
    0c2f26d40d9d18e3089c6d836b64231c4f7e0c1ca41686fe7c9d1eb495dc6bbe
)

pkgver() {
    cd "$_pkgname"
    git describe --long --abbrev=7  --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$_pkgname"

    make prefix=/usr
}

package() {
    cd "$_pkgname"

    make prefix=/usr DESTDIR="$pkgdir/" install
    install -Dm644 "$srcdir"/udev.rules "$pkgdir/usr/lib/udev/rules.d/65-$_pkgname.rules"
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
