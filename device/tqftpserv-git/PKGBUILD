_mode=host
_crossdirect=false
_pkgname=tqftpserv
pkgname="$_pkgname-git"
pkgdesc="Trivial File Transfer Protocol server over AF_QIPCRTR"
pkgver=1.0.r37.g9fc5fdc
pkgrel=1
_arches=specific
arch=(aarch64)
license=(BSD-3-Clause)
url="https://github.com/linux-msm/$_pkgname"
provides=("$_pkgname")
makedepends=(
    git
    meson
    "qrtr-git>=1.1"
    zstd
)
_commit=9fc5fdc53b5bc3e8ffc0b8f285443e8d1917c5cb
source=("git+$url#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd "$_pkgname"
    git describe --long --abbrev=7  --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    arch-meson $_pkgname build
    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    install -Dm644 "$_pkgname"/LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/COPYING
}
