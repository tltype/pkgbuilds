# Maintainer: Raihan Ahamed <raihan1999ahamed@gmail.com>
# Co-Maintainer: Danct12 <danct12@disroot.org>
_mode=host
pkgname=purism-chatty
pkgdesc="SMS/MMS, Matrix and (optionally) XMPP messaging app"
pkgver=0.8.8.r3.gcb28ceb
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL)
url=https://gitlab.gnome.org/World/Chatty
depends=(
    cairo
    dconf
    evolution-data-server
    feedbackd
    gcc-libs
    gdk-pixbuf2
    glib2
    glibc
    gnome-desktop-4
    gstreamer
    gtk4
    gtksourceview5
    hicolor-icon-theme
    libadwaita
    libcmatrix
    libmm-glib
    libolm
    libphonenumber
    libpurple
    libspelling
    pango
    sqlite
)
makedepends=(
    appstream
    git
    glib2-devel
    itstool
    meson
    ninja
)
checkdepends=(
    xorg-server-xvfb
)
optdepends=(
    "libpurple-carbons: message carbons support"
    "libpurple-lurch: OMEMO encryption"
    "libpurple-xmpp-http-upload: XMPP HTTP File Upload support"
    "mmsd-tng: MMS support"
    "purple-telegram: Telegram chat protocol support"
)
_commit="cb28cebfb4c047809acb1930f98e8bd5e01e1e91"
source=("git+$url.git#commit=${_commit}")
sha256sums=(SKIP)

pkgver() {
    cd Chatty
    git describe --long --tags --abbrev=7 "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd Chatty
    git submodule update --init
}

build() {
    export CXXFLAGS+=" -D_GTHREAD_USE_COND_INIT_FUNC"
    arch-meson Chatty output
    ninja -C output
}

# FAIL: secure memory pool is not locked while in FIPS mode
#check() {
#    xvfb-run ninja -C output test
#}

package() {
    DESTDIR="$pkgdir" ninja -C output install
}
