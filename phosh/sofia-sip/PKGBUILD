# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
_mode=host
pkgname=sofia-sip
pkgdesc="An open-source SIP User-Agent library"
pkgver=1.13.17.r47.g0106ad0
pkgrel=1
_arches=all
arch=(
    x86_64
    i686
    armv7h
    aarch64
)
license=(LGPL)
url="http://sofia-sip.sourceforge.net"
depends=(
    glib2
    openssl
    gawk
)
_commit="0106ad0e34a13485a4de79496589abf6949772dd"
source=(
    "git+https://github.com/freeswitch/$pkgname.git#commit=$_commit"
)
sha256sums=(SKIP)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd $pkgname
    ./autogen.sh
}

build() {
    cd $pkgname
    ./configure --prefix=/usr --with-openssl
    # Fight unused direct deps
    sed -i -e "s/ -shared / $LDFLAGS\0 /g" libtool
    make
}

package() {
    cd $pkgname
    make DESTDIR="$pkgdir" install
}
