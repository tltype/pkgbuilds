# Maintainer: Danct12 <danct12@disroot.org>
_mode=host
_crossdirect=false
pkgname=hfd-service
pkgdesc="Service that manages human feedback devices on mobile devices"
pkgver=0.2.3.r0.g98028b7
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(LGPL3)
depends=(
    accountsservice
    qt5-base
    qt5-feedback
    lomiri-deviceinfo
    yaml-cpp
)
makedepends=(
    cmake
    "cmake-extras>=1.8"
)
_commit=98028b740721dc8697602a3e6b0d6facf92f47f5
source=(
    "git+https://gitlab.com/ubports/development/core/hfd-service.git#commit=$_commit"
    plugin_install_dir.patch
)
sha256sums=(
    SKIP
    e51fb9959bbd2141b7fe70e2398d36bb572212f71bb9969f45ba0af915697315
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "$pkgname"
    patch -p1 -N < ../plugin_install_dir.patch
}

build() {
    cmake -B build -S "$pkgname" \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_LIBEXECDIR=bin \
        -DENABLE_LIBHYBRIS=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
