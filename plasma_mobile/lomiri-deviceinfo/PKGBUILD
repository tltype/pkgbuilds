# Maintainer: Devin Lin <devin@kde.org>
# Adapted for Kupfer Linux: @comradeFreeman
_mode=host
_crossdirect=false
pkgname=lomiri-deviceinfo
pkgdesc="Library to detect and configure devices"
pkgver=0.2.3.r0.g4423aab
pkgrel=1
_arches=specific
arch=(
    x86_64
    aarch64
    armv7h
)
license=(GPL-3.0-only)
url="https://gitlab.com/ubports/development/core/deviceinfo"
depends=(
    gtest
)
makedepends=(
    cmake
    extra-cmake-modules
    gtest
    samurai
    yaml-cpp
)
_commit=4423aab5a9f25ac1f6baf0623c7cf7a040d254ff
source=("git+$url.git#commit=$_commit")
sha256sums=(SKIP)

pkgver() {
    cd deviceinfo
    git describe --long --abbrev=7  --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S deviceinfo \
    -DDISABLE_TESTS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_SHARED_LIBS=True \
    -DCMAKE_BUILD_TYPE=Release \
    -DCONFIG_PATH=/etc/deviceinfo \
    -DCMAKE_POLICY_VERSION_MINIMUM=3.5
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
