# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Furkan Kardame <tech@fkardame.com>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
_crossdirect=false
pkgname=plasma-dialer
pkgdesc="Phone Dialer for Plasma Mobile"
pkgver=6.4.2.r0.gac7e1c3
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    qt6-base
    qt6-declarative
    kdbusaddons
    desktop-file-utils
    ki18n
    kcontacts
    kpeople
    knotifications
    kio
    kirigami-addons
    plasma-wayland-protocols
    modemmanager-qt
    libphonenumber
    callaudiod
)
optdepends=(
    "vulkan-headers: Vulkan header files and API registry"
)
makedepends=(
    cmake
    extra-cmake-modules
    kdoctools
)
_commit=ac7e1c3b9872afcb8da764b9ccadfe53e2c3aa2e
source=(
    "git+https://invent.kde.org/plasma-mobile/plasma-dialer.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../$pkgname \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_INSTALL_DIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DCMAKE_BUILD_TYPE=Release
    make
}

check() {
    cd build
    ctest --output-on-failure
}

package() {
    cd build
    make DESTDIR="$pkgdir/" install
}
