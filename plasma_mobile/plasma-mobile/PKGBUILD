# Contributor: Devin Lin <devin@kde.org>
_mode=host
_crossdirect=false
pkgname=plasma-mobile
pkgdesc="Plasma Mobile shell components."
pkgver=6.4.2.r0.g975aac6
pkgrel=1
_arches=all
arch=(
    aarch64
    x86_64
    armv7h
)
license=(GPL-2.0-or-later)
depends=(
    plasma-nano
    plasma-nm
    plasma-pa
    bluez-qt
    powerdevil
    modemmanager-qt
    "qqc2-breeze-style>=6.4.2"
    kirigami-addons
    maliit-keyboard
    kpipewire
    plasma-settings
    kwin
)
optdepends=(
    "plasma-settings: Settings application for Plasma Mobile"
    "plasma-dialer: Phone application"
    "plasma-workspace-wallpapers: A large wallpaper selection for Plasma"
)
makedepends=(
    cmake
    extra-cmake-modules
)
_commit=975aac6efed75d6694abcd1b22d6dc0fe8001c0d
source=(
    "git+https://invent.kde.org/plasma/plasma-mobile.git#commit=$_commit"
    plasma-mobile.service
)
sha256sums=(
    SKIP
    4e09f7eeae0d4c9fc7b1a9bbd1245a74809110999eadde85faba692556dec3de
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p build
}

build() {
    cmake -B build -S "${pkgname}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DCMAKE_INSTALL_LIBEXECDIR=lib \
        -DBUILD_TESTING=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
    
    install -Dm644 "$srcdir"/plasma-mobile.service \
        "$pkgdir"/usr/lib/systemd/system/plasma-mobile.service
}
