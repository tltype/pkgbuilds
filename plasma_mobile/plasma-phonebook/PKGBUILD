# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: dodgejcr@gmail.com
# Contributor: Bhushan Shah <bhush94 at gmail dot com>
# Contributor: Devin Lin <devin@kde.org>
_mode=host
_crossdirect=false
pkgname=plasma-phonebook
pkgdesc="Phonebook application for Mobile Devices running Plasma"
pkgver=24.02.0.r67.gefe6130
pkgrel=1
_arches=all
arch=(
    x86_64
    armv7h
    aarch64
)
license=(GPL-3.0-or-later)
depends=(
    ki18n
    kcoreaddons
    kconfig
    kpeople
    kcontacts
    kirigami
    kirigami-addons
    qt6-svg
    qt6-tools
)
makedepends=(
    cmake
    extra-cmake-modules
    kdoctools
    git
)
_commit=efe6130ee76e91d016b794e074f63a80066fb526
source=(
    "git+https://invent.kde.org/plasma-mobile/plasma-phonebook.git#commit=$_commit"
)
sha256sums=(
    SKIP
)

pkgver() {
    cd "$pkgname"
    git describe --long --abbrev=7 --tags "$_commit" | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p build
}

build() {
    cd build
    cmake ../plasma-phonebook \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=OFF \
        -DKDE_INSTALL_LIBDIR=lib \
        -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
        -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir/" install
}
